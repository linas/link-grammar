
set key left top
set term png

# -----------
set out 'dj-ctr.png'
set title "Parse Time vs Num connectors"
set ylabel "Total Parse time (seconds)"
set logscale y
set xlabel "Num Connectors"
plot "dj.dat" using 4:12 with points title ""

# -----------
set out 'dj-dj-ctr.png'
set title "Num disjuncts vs Num connectors"
set ylabel "Num Connectors"
unset logscale y
set xlabel "Num Disjuncts"
plot "dj.dat" using 2:4 with points title ""

# -----------
set out 'dj-dj-cho.png'
set title "Alloced Parse choices vs Num disjuncts"
set ylabel "Number of Parse choice alloced"
set logscale x
set logscale y
set xlabel "Number of Disjuncts (#dj)"
plot "dj.dat" using 2:8 with points title "",\
     ((0.16 * x)**2) with lines title "square of #dj"
#     "dj.dat" using 2:((0.16 * $2)**2) with lines title "square of #dj"


# -----------
set out 'dj-dj.png'
set title "Parse time vs Disjuncts"
set ylabel "Total Parse Time (seconds)"
set logscale x
set logscale y
set xlabel "Number of Disjuncts"
plot "dj.dat" using 2:12 with points title "raw data", \
     ((1.5e-4 * x)**2) with lines title "square of #dj"
#     "dj.dat" using 2:((1.5e-4 *$2)**2) with points title "square of #dj"

# -----------
set out 'dj-tm-chob.png'
set title "Alloced Parse choice Bytes vs Parse Time"
set ylabel "Bytes alloc'ed Parse choice"
set logscale x
set logscale y
set xlabel "Parse Time (seconds)"
plot "dj.dat" using 12:(96 * $8) with points title "raw data", \
     (96 * 1.1e6* x) with lines title "linear"
#     "dj.dat" using 12:(96 * 1.1e6*$12) with points title "linear"

