
set key left top
set term png

# -----------
set out 'djc-dj-cnt.png'
set title "Alloced Table connectors vs Num disjuncts"
set ylabel "Number of Table connector"
set logscale x
set logscale y
set xrange [1000:]
set yrange [10000:3e5]
set xlabel "Number of Disjuncts (#dj)"
plot "djc.dat" using 3:7 with points title "third dataset",\
     (0.1 * (x**1.4)) with lines lw 2 title "0.1 * #dj^{1.4}" ,\
     (0.7 * (x**1.2)) with lines lw 2 title "0.7 * #dj^{1.2}"

unset xrange
unset yrange

# -----------
set out 'djc-dj-cnt-scaled.png'
set title "Alloced Table connectors vs (Disjuncts x log(words))"
set ylabel "Number of Table connector"
set logscale x
set logscale y
set xrange [1000:]
set yrange [10000:2e6]
set xlabel "Disjuncts x log(words)"
f(x)=1.0/x
f(x)=sqrt(x)
f(x)=log(x)
#f(x)=1.0
plot "djc.dat" using (f(9) * $3):7 with points title "third MST dataset",\
     "dj-1500.dat" using (f(9) * $3):7 with points title "lim=1500 MST dataset",\
     "pandp.dat" using (f($3) * $5):11 with points title "Pride Prejudice",\
     (0.3 * (x**1.2)) with lines lw 2 title "0.3 * x^{1.2}", \
     (0.015 * x**1.5) with lines lw 2 title "0.015 x^{1.5}"

unset xrange
unset yrange

# -----------
set out 'djc-dj-buck.png'
set title "Alloced Buckets vs Num disjuncts"
set ylabel "Number of Buckets alloced"
set logscale x
set logscale y
set xlabel "Number of Disjuncts (#dj)"
plot "djc.dat" using 3:9 with points title "third dataset",\
     (3e-2 * x**1.5) with lines title "#dj^{1.5}"

# -----------
set out 'djc-cnt-buck.png'
set title "Alloced Buckets vs Table connectors"
set ylabel "Number of Buckets alloced"
set logscale x
set logscale y
set xlabel "Number of Table connectors"
plot "djc.dat" using 5:9 with points title "third dataset"

